import org.jetbrains.kotlin.gradle.tasks.KotlinCompile
buildscript {
    ext {
        kotlin_version = "1.9.21"
        corutines_version = "1.7.3"
        spring_boot_version = "3.3.0"
        spring_cloud_version = "2023.0.0"
        telegrambots_version = "6.8.0"
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_version")
    }
}
plugins {
    id "org.jetbrains.kotlin.jvm" version "$kotlin_version"
    id "org.jetbrains.kotlin.plugin.spring" version "$kotlin_version"
    id 'org.springframework.boot' version "$spring_boot_version"
    id "io.spring.dependency-management" version "1.1.0"
    id "nebula.release" version "19.0.6"
}

sourceCompatibility = JavaVersion.VERSION_17

ext {
}
apply plugin: "io.spring.dependency-management"

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${spring_cloud_version}"
    }
}
dependencies {
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$corutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-slf4j:$corutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-reactor:$corutines_version"
    implementation 'io.github.microutils:kotlin-logging-jvm:3.0.5'
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-configuration-processor"
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation "com.github.ben-manes.caffeine:caffeine:3.1.8"
    implementation "org.springframework:spring-context-support"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin"
    implementation "org.telegram:telegrambots-spring-boot-starter:$telegrambots_version"
    implementation "org.telegram:telegrambotsextensions:$telegrambots_version"
    implementation 'org.apache.commons:commons-lang3:3.14.0'
    implementation "com.bucket4j:bucket4j-core:8.7.0"
    implementation 'org.jsoup:jsoup:1.17.1'
}

repositories {
    mavenCentral()
}

task distrib(type: Zip, dependsOn: bootJar) {
    def baseName = "${project.name}-${project.version}"

    into("$baseName/") {
        from('build/libs') {
            include "${baseName}.jar"
        }
        from("deploy/common")
    }

    def buildType = "snapshot"
    if (project.version.toString().matches("\\d+\\.\\d+\\.\\d+")) {
        buildType = "production"
    }

    into("$baseName/") {
        from("deploy/$buildType/") {
            include 'docker-compose.yml'
        }
    }
    into("$baseName/credentials") {
        from("deploy/$buildType/") {
            include '*.env'
        }
    }

    archiveFileName = provider { "${baseName}.zip" }
}

build.dependsOn(distrib)

tasks.withType(KotlinCompile) {
    kotlinOptions {
        freeCompilerArgs = ['-Xjsr305=strict']
        jvmTarget = '17'
    }
}
